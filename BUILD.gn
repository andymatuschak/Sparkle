# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at http://mozilla.org/MPL/2.0/.

assert(is_mac)

group("sparkle_compile") {
  public_configs = [ ":sparkle_browser_build_config" ]
  public_deps = [ ":sparkle_framework" ]
}

group("sparkle_link") {
  public_configs = [ ":sparkle_link_config" ]
}

bundle_data("sparkle_framework") {
  public_deps = [ ":build_sparkle_framework" ]

  sources = [ "$root_out_dir/Sparkle.framework" ]
  outputs = [ "{{bundle_contents_dir}}/Frameworks/{{source_file_part}}" ]
}

bundle_data("sparkle_resources") {
  sources = [ "dsa_pub.pem" ]
  outputs = [ "{{bundle_resources_dir}}/{{source_file_part}}" ]
}

# Configuration for building chrome/browser static library.
config("sparkle_browser_build_config") {
  libs = [ "Sparkle.framework" ]

  cflags = [ "-F" + rebase_path(root_out_dir, root_build_dir) ]
}

# Configuration for link sparkle into brave framework.
config("sparkle_link_config") {
  ldflags = [
    "-Wl,-F" + rebase_path(root_out_dir, root_build_dir),
    "-Wl,-rpath,@loader_path/Frameworks",
    "-Wl,-rpath,@loader_path/.",
  ]
}

action("build_sparkle_framework") {
  script="build_sparkle_framework.py"

  outputs = [ "$root_out_dir/Sparkle.framework" ]
}

